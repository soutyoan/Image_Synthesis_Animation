#version 430 core

layout(binding = 0, rgba32f) uniform writeonly image2D framebuffer;

uniform sampler2D colorTexture;
uniform mat4 mat_inverse;
uniform mat4 persp_inverse;
uniform float lightIntensity;
uniform vec3 lightPosition;
uniform float shininess;
uniform float eta;
uniform vec3 bbmin;
uniform vec3 bbmax;
uniform float radius;
uniform float groundDistance;
uniform vec3 center;
uniform int pixelSize;
uniform int shift_x;
uniform int shift_y;

#define MAX_SCENE_BOUNDS    1
#define EPS                 0.001


struct hitinfo_t {
    vec4 t;
    int hit_vptr;
};

struct intersectionItem {
    vec4 origin;
    vec4 dir;
    vec4 point_hit;
    vec4 normal;
    vec4 color;
};

float max3 (vec3 v) {
  return max (max (v.x, v.y), v.z);
}

float min3 (vec3 v) {
  return min (min (v.x, v.y), v.z);
}

layout (std430, binding = 1) buffer Vertices
{
    vec4 vertices[];
};

layout (std430, binding = 2) buffer Normals
{
    vec4 normals[];
};

layout (std430, binding = 3) buffer Colors
{
    vec4 colors[];
};

layout (std430, binding = 4) buffer Meshes
{
    int indices[];
};

bool intersectBoundingBox(vec4 origin, vec4 dir) {
	vec3 tmin = (origin.xyz - bbmin.xyz) / dir.xyz;
    vec3 tmax = (origin.xyz - bbmax.xyz) / dir.xyz;

    float in_val = max3(vec3(min(tmin.x, tmax.x), min(tmin.y, tmax.y), min(tmin.z, tmax.z)));
    float out_val = min3(vec3(max(tmin.x, tmax.x), max(tmin.y, tmax.y), max(tmin.z, tmax.z)));
    return out_val > in_val;
}

bool intersectPlane(vec4 origin, vec4 dir, out vec4 dist)
{
    vec4 Q = vec4(0, -groundDistance, 0, 1);
    vec4 normal_plane = vec4(0, 1, 0, 0);
    float lambda_plane = 0.0;
    if (dot(dir, normal_plane)!=0) {
        float lambda_plane = dot((Q - origin).xyz, normal_plane.xyz) / dot(normalize(dir).xyz, normal_plane.xyz);
        if (lambda_plane >= EPS) {
            dist.xyz = origin.xyz + lambda_plane * dir.xyz;
            dist.w = lambda_plane;
            return true;
        }
    }
    return false;
}

bool intersectTriangle(vec4 origin, vec4 dir, int ptr, out vec4 dist)
{
	vec3 V0 = vertices[indices[ptr]].xyz;
    vec3 V1 = vertices[indices[ptr + 1]].xyz;
    vec3 V2 = vertices[indices[ptr + 2]].xyz;
    vec3 e1 = V1 - V0;
    vec3 e2 = V2 - V0;
    vec3 t = origin.xyz - V0;
    vec3 u = normalize(dir.xyz);
    vec3 p = cross(dir.xyz, e2);
    vec3 q = cross(t, e1);
    if (dot(p, e1) == 0) {
        return false;
    } else {
        vec3 sol = 1 / (dot(p, e1)) * vec3(dot(q, e2), dot(p, t), dot(q, u));
        float lambda = sol[0];
        if (lambda <= EPS) {
            return false;
        }
        float alpha = sol[1];
        if (alpha<-EPS || alpha>1+EPS) {
            return false;
        }
        float beta = sol[2];
        if (beta<-EPS || beta>1+EPS) {
            return false;
        }
        if ((1 - alpha - beta)<-EPS || (1-alpha-beta)>1+EPS) {
            return false;
        }
        dist[0] = alpha;
        dist[1] = beta;
        dist[2] = 1 - alpha - beta;
        dist[3] = lambda;
        return true;
        }
}

vec4 interpolateNormal(hitinfo_t h)
{
	if (h.hit_vptr == -1) {
        return vec4(0, 1, 0, 0);
        }
        else if (h.hit_vptr >= 0) {
        vec4 n0 = normals[indices[h.hit_vptr]];
        vec4 n1 = normals[indices[h.hit_vptr+1]];
        vec4 n2 = normals[indices[h.hit_vptr+2]];
        return normalize(h.t.x * n1 + h.t.y * n2 + h.t.z * n0);
    }
    return vec4(1);
}

vec4 interpolateColor(hitinfo_t h)
{
    if (h.hit_vptr == -1) {
        //return vec4(0.8, 0, 0, 1);
        return texture2D(colorTexture, h.t.xz);
    }
	else if (h.hit_vptr>=0) {
        vec4 c0 = colors[indices[h.hit_vptr]];
        vec4 c1 = colors[indices[h.hit_vptr+1]];
        vec4 c2 = colors[indices[h.hit_vptr+2]];
        return h.t.x * c1 + h.t.y * c2 + h.t.z * c0;
    }
	return vec4(0, 1, 0, 1);
}


bool isIntersected(vec4 origin, vec4 dir, out hitinfo_t h)
{
    vec4 dist; // stores barycentric coord + lambda distance
    h.t.w = 10000;
    vec4 dist_plane; // store points coordinates + lambda value for plane
	bool hit = intersectPlane(origin, dir, dist_plane);
    if (hit) {
        h.t = dist_plane;
        h.hit_vptr = -1;
    }

    // check the intersection with plane
	if(intersectBoundingBox(origin, dir)) {
		for(int j = 0; j < indices.length(); j += 3) {
			if(intersectTriangle(origin, dir, j, dist)) {
                hit = true;
                if (h.t.w > dist.w) {
                    h.t = dist;
                    h.hit_vptr = j;
                }
            }
		}
	}
	return hit;
}

const int MAX_TRACE = 1;

float cos_angle(vec4 vectA, vec4 vectB)
{
    return dot(normalize(vectA), normalize(vectB));
}

float F( float cos_theta )
{
    float inv_eta = 1/eta;
    if ((pow(inv_eta, 2) - (1 - pow(cos_theta, 2))) < 0) {
        return 1.0;
    }
    float ci = sqrt(pow(inv_eta, 2) - (1 - pow(cos_theta, 2)));
    float Fs = pow(abs((cos_theta - ci) / (cos_theta + ci)), 2);
    float Fp = pow(abs((pow(inv_eta, 2) * cos_theta - ci) / (pow(inv_eta, 2) * cos_theta + ci)), 2);

    return (Fs + Fp) / 2;
}

vec4 phongModel(vec4 vertLight, vec4 vertNormal, vec4 vertEye, vec4 vertColor, float lightIntensity, bool only_ambiant) {
    // Assuming that the following vectors are already normalized :
    // vertLight, vertNormal, vertEye
    float ka = 0.2;
    vec4 Ca = ka * lightIntensity * vertColor;
    if (only_ambiant) {
        return Ca;
    }

    float kd = 0.5;
    vec4 Cd = kd * lightIntensity * vertColor * max(dot(vertNormal, -vertLight), 0);

    vec4 halfVector = normalize(vertEye - vertLight);
    float cos_theta_d = cos_angle(halfVector, -vertLight);
    vec4 Cs =  F(cos_theta_d) * lightIntensity * pow(max(dot(vertNormal, halfVector), 0), shininess) * vertColor;

    return Ca + Cd + Cs;
}


vec4 trace(vec4 origin, vec4 dir)
{
    hitinfo_t h;
    intersectionItem intersections[int(MAX_SCENE_BOUNDS)];
    int nb_bounds = 0;
    vec4 current_origin = vec4(origin.xyzw);
    vec4 current_dir = vec4(dir.xyzw);
    vec4 result = vec4(0,0,0,1);
    // hit either the plane or a triangle
    while (nb_bounds < MAX_SCENE_BOUNDS && isIntersected(current_origin, current_dir, h)) {
        intersections[nb_bounds].origin = vec4(current_origin);
        current_origin += h.t.w * current_dir;
        intersections[nb_bounds].point_hit = vec4(current_origin);
        vec4 vertNormal = interpolateNormal(h);
        vec4 vertColor = interpolateColor(h);
        if (h.hit_vptr==-1) {
            vertColor = texture2D(colorTexture, dir.xz);
        }
        intersections[nb_bounds].normal = vec4(vertNormal);
        intersections[nb_bounds].color = vec4(vertColor);
        vec4 vertEye = normalize(origin - current_origin);
        vec4 vertLight = normalize(current_origin - vec4(lightPosition.xyz, 1));
        current_dir = -vertLight;
        intersections[nb_bounds].dir = vec4(current_dir);
        if (dot(intersections[nb_bounds].normal, intersections[nb_bounds].dir)<0) {
            break;
        }
        nb_bounds ++;

    }
    float current_Fresnel = 1.0;
    for (int s=nb_bounds-1; s>=0; s--) {

        vec4 vertEye = normalize(intersections[s].origin - intersections[s].point_hit);
        bool only_ambiant = (dot(intersections[s].normal, intersections[s].dir) > 0) && isIntersected(intersections[s].point_hit, intersections[s].dir, h);
        vec4 auxDir = vec4(intersections[s].dir.xyzw);
        // if (s<nb_bounds-1) {
        //     auxDir = intersections[s+1].dir;
        // }
        float cos_theta = dot(intersections[s].normal, auxDir);

        current_Fresnel *= F(cos_theta);
        if (F(cos_theta) > 1 ){
            result = vec4(0,1,0,1);
        }
        result = phongModel(-intersections[s].dir, intersections[s].normal, vertEye, intersections[s].color, lightIntensity, only_ambiant) + current_Fresnel * result ;

    }

    return result;
}

layout (local_size_x = 8, local_size_y = 8) in;

void main(void) {
    ivec2 pix = ivec2(gl_GlobalInvocationID.xy) * pixelSize + ivec2(shift_x, shift_y);
    ivec2 size = imageSize(framebuffer);

    if (pix.x >= size.x || pix.y >= size.y) {
        return;
    }

    vec2 pos = pix / (size - vec2(0.5,0.5));

    // pos in [0,1]^2 Need it in [-1,1]^2:
    pos = (2 * pos - vec2(1.,1.));

    // Step 1: I need pixel coordinates.
    vec4 worldPos = vec4(pos.x, pos.y, 1.0, 1.0);
    worldPos = persp_inverse * worldPos;
    worldPos /= worldPos.w;
    worldPos.w = 0;
    worldPos = normalize(worldPos);
    // Step 2: ray direction:
    vec4 dir = normalize((mat_inverse * worldPos));
    vec4 eye = (mat_inverse * vec4(0, 0, 0, 1));
    vec4 color = trace(eye, dir);

    imageStore(framebuffer, pix, color);
}
